apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'application'
apply plugin: 'distribution'

apply from: file('gradle/docs.gradle')

group 'sbi'
version '1.0-SNAPSHOT'
project.buildDir = '/target'

mainClassName = "IPL"
applicationDefaultJvmArgs = ["-Dide=true -Ddebug=true"]

project.sourceCompatibility = 1.8
project.targetCompatibility = 1.8

sourceSets {
    main {
        output.resourcesDir = '/target/classes'
        output.classesDir   = '/target/classes'
    }
}

/**
 * Workaround for double files in jar when manually setting output dirs
 */
jar.doFirst {
    sourceSets.main.output.resourcesDir = "/does/not/exist"
}
jar.doLast {
    sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
}

jar {
    from rootProject.file('NOTICE')
    manifest {
        attributes 'Main-Class': 'IPL'
    }
}

checkstyle {
    ignoreFailures = true
    // showViolations = false
}

//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: "http://url.to/the/repository")
//        }
//    }
//}

configurations {
    ipl
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
}

dependencies {
    testCompile 'junit:junit:4.11'

    compile project(':Model')
    compile 'com.scireum:sirius-kernel:2.0-SNAPSHOT'
    compile 'com.scireum:sirius-ipl:1.0'
    ipl 'com.scireum:sirius-ipl:1.0'

//    fileTree(dir: 'lib', include: '**/*.jar')
}

/**
 * Extracting contents of the IPL dependency to the build directory
 */
task extractIPL(type: Copy) {
    from {
        configurations.ipl.collect { zipTree(it) }
    }
    into file("${buildDir}/ipl")
}

/**
 * List jar names of all dependencies used
 */
task listJars << {
    configurations.compile.each {
        File file -> println file.name
    }
}

/**
 * Specify the gradle wrapper version
 */
task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

/**
 * Copy all runtime dependencies to folder in build dir
 */
task copyDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/output/lib"
}

/**
 * Creation of assembly ZIP file with distribution plugin
 */
distZip.dependsOn { extractIPL }

distributions {
    main {
        baseName = "GradleExample"
        contents {
            from "$buildDir/ipl"
            from ("$buildDir/classes") {
                into "app"
            }
//          exclude "$buildDir/classes"
        }
    }
}

/**
 * Some interesting links regarding gradle
 */
// http://toastdroid.com/2014/03/28/customizing-your-build-with-gradle/
// https://jitpack.io/
// https://github.com/johnrengelman/shadow
// http://www.mkyong.com/gradle/gradle-create-a-jar-file-with-dependencies/
// http://stackoverflow.com/questions/4871656/using-gradle-to-build-a-jar-with-dependencies
// http://blog.denevell.org/gradle-hello-world-java-fat-jar.html
// http://fuzzydroid.blogspot.com/2015/09/some-gradle-tricks-for-android.html